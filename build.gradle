plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'jacoco'
	id 'org.sonarqube' version '5.0.0.4638'
	id 'checkstyle'
	id 'org.owasp.dependencycheck' version '9.1.0'
	id 'com.github.ben-manes.versions' version "0.51.0"
	id 'se.patrikerdes.use-latest-versions' version '0.2.18'
}

group = 'es.in2'
version = '1.0.0'

java {
	sourceCompatibility = '17'
}

jacoco {
	toolVersion = "0.8.11"
}

checkstyle {
	configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

checkstyleMain {
	source ='src/main/java'
}

checkstyleTest {
	source ='src/test/java'
}

sonar {
	properties {
		property "sonar.projectName", "Issuer DSS"
		property "sonar.projectKey", "in2workspace_in2-dome-dss"
		property "sonar.organization", "in2workspace"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.coverage.exclusions",
				"src/main/java/es/in2/issuer/DssApplication.java, "
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {

	// Spring
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

	// Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	// Documentation
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

	// JSON
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'


	// Logback
	implementation 'ch.qos.logback:logback-classic:1.4.11'
	implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
	implementation 'org.bouncycastle:bcpkix-jdk18on:1.77'
	implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'
	implementation 'javax.xml.bind:jaxb-api:2.3.1'
	implementation 'com.sun.xml.bind:jaxb-impl:2.3.1'
	implementation 'org.javassist:javassist:3.25.0-GA'

	//Europa DSS https://ec.europa.eu/digital-building-blocks/DSS/webapp-demo/doc/dss-documentation.html
	implementation("eu.europa.ec.joinup.sd-dss:dss-jades:5.13.RC1")
	implementation("eu.europa.ec.joinup.sd-dss:dss-token:5.13.RC1")
	implementation 'eu.europa.ec.joinup.sd-dss:dss-service:5.13.RC1'
	implementation 'eu.europa.ec.joinup.sd-dss:dss-utils-google-guava:5.13.RC1'
	implementation 'eu.europa.ec.joinup.sd-dss:dss-tsl-validation:5.13.RC1'

	// COSE
	implementation("io.github.ehn-digital-green-development:base45:0.0.3")
	implementation("org.apache.commons:commons-compress:1.21")
	implementation("com.upokecenter:cbor:4.5.2")
	implementation("com.augustcellars.cose:cose-java:1.1.0")

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// AZURE
	implementation 'com.azure.spring:spring-cloud-azure-dependencies:5.7.0'
	implementation 'com.azure.spring:spring-cloud-azure-starter-appconfiguration:5.7.0'

}

tasks.named('compileJava') {
	options.annotationProcessorPath = configurations.annotationProcessor
	inputs.files(tasks.named('processResources'))
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy(tasks.jacocoTestReport)
}

tasks.jacocoTestReport {
	dependsOn(tasks.test)
	reports {
		xml.required.set(true)
		csv.required.set(false)
		html.outputLocation.set(layout.buildDirectory.dir("jacocoHtml"))
	}

	classDirectories.setFrom(files(classDirectories.files.collect {
		fileTree(dir: it, exclude: [
					'**/In2RemoteSignatureApplication**',
					'**/config/**',
					'**/enums/**'
			])
		}))
}

tasks.register('printVersion') {
	doLast {
		println version
	}
}

tasks.register('printProjectName') {
	doLast {
		println rootProject.name
	}
}


